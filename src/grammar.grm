"Name"      = 'Think out Programming Language'
"Author"    =  'Beatriz de Brito, Cássio Santos, Dermeval Neves, Elvis Huges, Leandro Sampaio, Tayane Cerqueira'
"Version"   = '1.0'
"About"     = 'Linguagem desenvolvida para a disciplina MI - Processadores de Linguagem de Programação da UEFS - 2018.1'

"Case Sensitive"    = True
"Start Symbol"      = <Programa>

! =================================================
! Conjuntos de Caracteres
! =================================================

{CabecaDoIdentificador} = {Letter}
{CaudaDoIdentificador}  = {Alphanumeric} + [_]
{StringSimbolos}= {Printable}-["\]

! =================================================
! Símbolos Terminais
! =================================================

identificador   = {CabecaDoIdentificador}{CaudaDoIdentificador}*
Digitos         = ( '-' )? {Number}+ ( '.' {Number}+ )?
CadeiaDeCaracteres        = '"' ({StringSimbolos} | '\' {Printable})* '"'

! =================================================
! Regras
! =================================================

! -------------------------------------------------
! Declaração de Função, Procedimentos, método de início
! -------------------------------------------------

<DeclaracaoDeFuncao>        ::= 'function' <FuncID> '(' <FuncaoProcedimentoFim>

<DeclaracaoDeProcedimento>  ::= 'procedure' Identificador '(' <FuncaoProcedimentoFim>

<FuncaoProcedimentoFim>     ::= <Parametros> ')' <Bloco> 
                            | ')' <Bloco>

<DeclaracaoDeInicio>    ::= 'start' '(' ')' <Bloco>

<Bloco>     ::= '{' <BlocoAux>
<BlocoAux>  ::= <ListaDeIntrucoes> '}'
            | '}'

<FuncID>    ::= <Tipo> Identificador

<Parametros>    ::= <Parametro> <ParametrosAux>
<ParametrosAux> ::= ',' <Parametros>
                | <> ! Produção Vazia

<Parametro>     ::= <Tipo> Identificador

! -------------------------------------------------
! Declaração de Tipo (Typedef)
! -------------------------------------------------

<DeclaracaoDeTypedef>       ::= 'typedef' <DeclaracaoDeTypedefAux>
<DeclaracaoDeTypedefAux>    ::= <Tipo> Identificador ';'

! -------------------------------------------------
! Base (Core)
! -------------------------------------------------

<Programa>      ::= <Declaracao> <ProgramaAux>

<ProgramaAux>   ::= <Programa>
                | <> ! Produção Vazia

! Aqui devem ficar apenas as estruturas que podem ser criadas fora de um Bloco
! Exemplo: Funções, Variaveis, Estruturas, Typedef, Procedimentos, Start
<Declaracao>    ::= <DeclaracaoDeFuncao>
                | <DeclaracaoDeProcedimento>
                | <DeclaracaoDeInicio>
                | <DeclaracaoDeVar>
                | <DeclaracaoDeConst>
                | <DeclaracaoDeStruct>
                | <DeclaracaoDeTypedef>

! -------------------------------------------------
! Instruções
! -------------------------------------------------

<ListaDeIntrucoes>      ::= <Instrucao> <ListaDeIntrucoesAux>
<ListaDeIntrucoesAux>   ::= <ListaDeIntrucoes>
                        | <> ! Produção Vazia

! Instrução deve ser capaz de produzir todos os comandos que podem ficar dentro de um bloco
! Exemplo dos blocos: if, função, procedimento, while, etc... 
! Exemplo das instruções: if, while, Chamada de função, print, scan, etc... 
<Instrucao> ::= <InstrucaoNormal>
            | <EstruturaCondicional>
            | <While>
            | <DeclaracaoDeVar>
            | <DeclaracaoDeTypedef>

! Instrução Normal são as insrtuções de unica linha que terminal com ';'
! Exemplo: a = b;, print();, scan();, chamadaDeFuncao();
<InstrucaoNormal>   ::= <OperacaoDeAtribuicao> ';'
                    | <DeclaracaoDeStruct> ';'
                    | <InstrucaoDeRetorno> ';'
                    | <Print> ';'
                    | <Scan> ';'

<InstrucaoDeRetorno>    ::= 'return' <InstrucaoDeRetornoAux>
<InstrucaoDeRetornoAux> ::= <Expressao> 
                        | <> ! Produção Vazia

! -------------------------------------------------
! Tipos
! -------------------------------------------------

<Tipo>      ::= <TipoBase> <TipoAux>
<TipoAux>   ::= <TipoVetorDeclarando>
            | <> ! Produção Vazia

<TipoBase>  ::= <Escalar>
            | <DeclaracaoDeStruct>
            | Identificador
            | 'struct' Identificador

<Escalar>   ::= 'int'
            | 'float'
            | 'bool'
            | 'string'

! Permite utilização de tipos vector, tando em inicialização de variáveis como em retorno de funções

<TipoVetorDeclarando>       ::= <TipoVetorDeclarado> <TipoVetorDeclarandoAux>
<TipoVetorDeclarandoAux>    ::= <TipoVetorDeclarando>
                            | <> ! Produção Vazia

<TipoVetorDeclarado>    ::= '[' ']'

! -------------------------------------------------
! Precedência de Operadores
! -------------------------------------------------

<OperacaoDeAtribuicao>  ::= <Final> '=' <Expressao>
                        | Identificador '=' <Expressao>
                        | <Expressao>


<ParametrosFuncao>      ::= <Expressao> <ParametrosFuncaoAux>
<ParametrosFuncaoAux>   ::= ',' <ParametrosFuncao>
                        | <> ! Produção Vazia

<Expressao>     ::= <OpE> <ExpressaoAux>
<ExpressaoAux>  ::= '||' <Expressao>
                | <> ! Produção Vazia

<OpE>   ::= <OpRelacional> <OpEAux>
<OpEAux> ::= '&&' <OpE>
        | <> ! Produção Vazia

<OpRelacional>      ::= <ValorRelacional> <OpRelacionalAux>
<OpRelacionalAux>   ::= <EscalarRelacional> <OpRelacional>
                    | <> ! Produção Vazia

<EscalarRelacional> ::= '!='
                    | '=='
                    | '<'
                    | '<='
                    |'>'
                    |'>='

<ValorRelacional> ::= <OpMult> <ValorRelacionalAux>
<ValorRelacionalAux>     ::= '+' <OpMult> <ValorRelacionalAux>
        | '-' <OpMult> <ValorRelacionalAux>
        | <> ! Produção Vazia

<OpMult>        ::= <OpUnary> <OpMultAux>
<OpMultAux>     ::= '*' <OpUnary> <OpMultAux>
                | '/' <OpUnary> <OpMultAux>
                | <> ! Produção Vazia

<OpUnary>       ::= '!' <OpUnary>
                | '++' <OpUnary>
                | '--' <OpUnary>
                | <Final> <SimboloUnario>
                | <Valor> <SimboloUnario>
<SimboloUnario> ::= '++'
                | '--'
                | <> ! Produção Vazia

<Final>     ::= Identificador <Acessando> 

<Acessando>     ::= <Acesso> <AcessandoAux>
<AcessandoAux>  ::= <Acessando>
                | <> ! Produção Vazia

<Acesso>        ::= '.' Identificador ! Acesso de campo
                | '[' <Expressao> ']' ! Acesso de array

! <Valor> deve ser o ultimo nível de precedência
<Valor>     ::= Identificador <ValorAux1>
            | '(' <Expressao> ')'
            | Digitos
            | CadeiaDeCaracteres
            | 'true'
            | 'false'
<ValorAux1> ::= '(' <ValorAux2>
            | <> ! Produção Vazia
<ValorAux2> ::=  <ParametrosFuncao> ')' ! Função ou procedimento com parametros
            | ')' ! Função ou procedimento sem parametros

! -------------------------------------------------------------------
! Declaração de Bloco Variáveis 
! -------------------------------------------------------------------
<DeclaracaoDeVar>               ::= 'var'  '{'  <DeclaracaoDeVariavelCorpo> '}'

<DeclaracaoDeVariavelCorpo>     ::= <DeclaracaoDeVariavelLinha> <DeclaracaoDeVariavelCorpoAux>
<DeclaracaoDeVariavelCorpoAux>  ::= <DeclaracaoDeVariavelCorpo>
                                | <> ! Produção Vazia 

<DeclaracaoDeVariavelLinha>     ::= <Tipo> <ExpressaoIdentificadoresVar>

<ExpressaoIdentificadoresVar>       ::= <ExpressaoIdentificadorVar> <ExpressaoIdentificadoresVarAux>
<ExpressaoIdentificadoresVarAux>    ::= ';'
                                    | ',' <ExpressaoIdentificadoresVar>

<ExpressaoIdentificadorVar>     ::= Identificador <ExpressaoIdentificadorVarAux>
<ExpressaoIdentificadorVarAux>  ::=  '=' <Expressao>
                                | <> ! Produção Vazia

! -------------------------------------------------------------------
! Declaração de Bloco Constante 
! -------------------------------------------------------------------

<DeclaracaoDeConst>             ::= 'const'  '{'  <DeclaracaoDeConstanteCorpo> '}'

<DeclaracaoDeConstanteCorpo>    ::= <DeclaracaoDeConstanteLinha> <DeclaracaoDeConstanteCorpoAux>
<DeclaracaoDeConstanteCorpoAux> ::= <DeclaracaoDeConstanteCorpo>
                                | <>

<DeclaracaoDeConstanteLinha>    ::= <Tipo> <ExpressaoIdentificadoresConst>

<ExpressaoIdentificadoresConst>     ::= <ExpressaoIdentificadorConst> <ExpressaoIdentificadoresConstAux>
<ExpressaoIdentificadoresConstAux>  ::= ';' 
                                    | ',' <ExpressaoIdentificadoresConst>

<ExpressaoIdentificadorConst>   ::= Identificador '=' <Expressao>

! -------------------------------------------------------------------
! Declaração de Bloco Struct 
! -------------------------------------------------------------------

<DeclaracaoDeStruct>    ::= 'struct' <DeclaracaoDeStructAux>
<DeclaracaoDeStructAux> ::= Identificador <Extends>'{'  <DeclaracaoDeStructCorpo> '}'
                        | <Extends>'{'  <DeclaracaoDeStructCorpo> '}'
<Extends>               ::= 'extends' Identificador
                        | <>

<DeclaracaoDeStructCorpo>       ::= <DeclaracaoDeStructLinha> <DeclaracaoDeStructCorpoAux>
<DeclaracaoDeStructCorpoAux>    ::= <DeclaracaoDeStructCorpo>
                                | <>

<DeclaracaoDeStructLinha>    ::= <Tipo> <ExpressaoIdentificadoresStruct>

<ExpressaoIdentificadoresStruct>    ::= <ExpressaoIdentificadorStruct> <ExpressaoIdentificadoresStructAux>
<ExpressaoIdentificadoresStructAux> ::= ';'
                                    | ',' <ExpressaoIdentificadoresStruct>

<ExpressaoIdentificadorStruct>   ::= Identificador

! -------------------------------------------------------------------
! Estruturas condicionais: If...Then, If...Then...Else
! -------------------------------------------------------------------

<EstruturaCondicional>      ::= <IfThen> <EstruturaCondicionalAux>
<EstruturaCondicionalAux>   ::= 'else' <Bloco>
                            | <>

<IfThen> ::= 'if' '(' <Expressao> ')' 'then' <Bloco>

! -------------------------------------------------------------------
! Estruturas de repetição: While
! -------------------------------------------------------------------

<While> ::= 'while' '(' <Expressao> ')' <Bloco>

! -------------------------------------------------------------------
! Print & Scan
! -------------------------------------------------------------------

<Print>         ::= 'print' '(' <Saida> <OutrasSaidas> ')'
<Saida>         ::= <Expressao>
<OutrasSaidas>  ::= ',' <Saida> <OutrasSaidas> 
                | <> ! Produção Vazia

<Scan>              ::= 'scan' '(' <Entrada> <OutrasEntradas> ')'
<Entrada>           ::= <Final> | Identificador
<OutrasEntradas>    ::= ',' <Entrada> <OutrasEntradas> 
                    | <> ! Produção Vazia
